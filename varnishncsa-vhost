#!/usr/bin/perl
use strict;
use warnings;
use Getopt::Std;
use Proc::Daemon;
$| = 1;
our $VERSION = "0.02";

=head1 NAME

varnishncsa-vhost - wrapper around varnishncsa tool to save varnish logs with virtualhost

=head1 SYNOPSIS

    $ varnishncsa-vhost [options] -- [varnishncsa-options]

=head1 DESCRIPTION

This simple script uses varnishncsa tool to provide a way to store varnish logs
with vhost, cause varnish by default until version 2.1.5 not allow to customize
log formats.

The output produced by this wrapper is the as 'combined' from Apache LogFormat
plus virtualhost. See apache config below as example used by me.

LogFormat "%v %h %l %u %t \"%r\" %>s %O \"%{Referer}i\" \"%{User-Agent}i\"" vhost_combined

=head1 OPTIONS

All options accepted by varnishncsa are accepted from varnishncsa-vhost,
options below are handled by varnishncsa-vhost, all others are directly passed
to varnishncsa and should be passed after '--' in command line.

=head2 -a

See varnishncsa manpage.

=head2 -D

See varnishncsa manpage.

=head2 -w logfile

See varnishncsa manpage.

=cut

# changes proccess name showed by `ps` (see perldoc perlvar)
$0 = "varnishncsa-vhost @ARGV";

our %opts;
getopts('aw:D', \%opts);

if ($opts{'D'}) {
   Proc::Daemon::Init() && exit;
}

if ($opts{'w'}) {
   if ($opts{'a'}) {
      open (STDOUT, '>>', $opts{'w'}) or die "Could not open logfile $opts{w}: $!\n";
   }
   else {
      open (STDOUT, '>', $opts{'w'}) or die "Could not open logfile $opts{w}: $!\n";
   }
}
open (COMMAND, "varnishncsa @ARGV | ") or die "Could not open varnishncsa: $!\n";
while (<COMMAND>) {
   if (m{(?:GET|POST|HEAD|PUT|DELETE|TRACE|CONNECT) https?://([^/?#]+)}o) {
      # print actual line plus host at begining
      print "$1 $_";
   }
   else {
      # print actual line without any modification
      print "$_";
   }
}
close COMMAND;
close STDOUT;

=head1 AUTHOR

Joenio Costa <joenio at colivre.coop.br>

=head1 COPYRIGHT

Copyright (c) 2010-2013 Colivre

http://www.colivre.coop.br

=head1 LICENSE

This is free software, licensed under GNU GPL2 or later.
